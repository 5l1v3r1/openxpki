#!/usr/bin/perl
#
# Written by Martin Bartosch for the OpenXPKI project 2006
# Copyright (c) 2006 by The OpenXPKI Project
# $Revision$
#

use strict;
use warnings;
use English;
use Getopt::Long qw( :config permute pass_through );
use Pod::Usage;
use Term::ReadLine;

use Smart::Comments;

use OpenXPKI::Debug;
use OpenXPKI::Client::CLI;

my $configfile;

$SIG{PIPE} = 'IGNORE';

my %params;
GetOptions(\%params,
	   qw(
	      help|?
	      man
	      socketfile=s
              session=s
              debug=i
	      ));

pod2usage(-exitstatus => 0, -verbose => 2) if $params{man};
pod2usage(-verbose => 1) if ($params{help});

if (defined $params{debug}) {
    $OpenXPKI::Debug::LEVEL{'.*'} = $params{debug};
}

my $cli = OpenXPKI::Client::CLI->new(
    {
	SOCKETFILE => $params{socketfile},
    });

my %args;
if (exists $params{session}) {
    $args{SESSION_ID} = $params{session};
}

$cli->init_session(\%args);
my $session_id = $cli->get_session_id();
### $session_id

if (scalar @ARGV == 0) {
    print "Entering interactive OpenXPKI shell.\n";
    print "Type 'help' for help, 'quit' to exit.\n\n";

    my $term = new Term::ReadLine 'OpenXPKI';
    my $OUT = $term->OUT || \*STDOUT;
    my $state = '';

  INTERACTION:
    while (1) {
	### $cli->get_communication_state()
	if ($cli->get_communication_state() eq 'can_receive') {
	    ### handle response from last command and render output...
	    my $response;
	    eval {
		$response = $cli->collect();
	    };
	    if (my $exc = OpenXPKI::Exception->caught()) {
		if ($exc->message() eq 'I18N_OPENXPKI_TRANSPORT_SIMPLE_CLIENT_READ_CLOSED_CONNECTION') {
		    print $OUT "Server terminated connection.\n";
		    last INTERACTION;
		    ### $session_id
		    # re-initialize session
# 		    $cli = OpenXPKI::Client::CLI->new(
# 			{
# 			    SOCKETFILE => $params{socketfile},
# 			});
# 		    $cli->init_session(
# 			{
# 			    SESSION_ID => $session_id,
# 			});
# 		    redo INTERACTION;
		}
		$exc->rethrow();
	    }
	    $cli->render($response) if defined $response;
	}

	my $prompt = 'OpenXPKI [' . $state . ']: ';
	my $line = $term->readline($prompt);
	last INTERACTION unless defined $line;  # accept EOF to leave shell

	my $cmd_res;
	eval {
	    $cmd_res = $cli->process_command($line, 
					     {
						 READLINE => $term,
					     }
		);
	};
	if ($EVAL_ERROR) {
	    print "error: $EVAL_ERROR\n";
	    next INTERACTION;
	}

	# command exit requested?
	last INTERACTION if (! defined $cmd_res);

	my @message;
	if (exists $cmd_res->{MESSAGE}) {
	    if (ref $cmd_res->{MESSAGE} eq '') {
		push @message, $cmd_res->{MESSAGE};
	    }
	    if (ref $cmd_res->{MESSAGE} eq 'ARRAY') {
		@message = @{$cmd_res->{MESSAGE}};
	    }
	}

	if (! defined $cmd_res->{ERROR} ||
	    ($cmd_res->{ERROR} == 0)) {
	    print $OUT join("\n", @message);
	    print "\n";
	} else {
	    print $OUT "ERROR $cmd_res->{ERROR}: ". join("\n", @message) . "\n";
	}

	if (exists $cmd_res->{SERVICE_COMMAND} &&
	    exists $cmd_res->{COMMAND}) {
	    print $OUT "Internal error: command implementation requested ambiguous message\n";
	    exit 1;
	}

	if (exists $cmd_res->{SERVICE_COMMAND} &&
	    (ref $cmd_res->{SERVICE_COMMAND} eq 'HASH')) {

	    ### send service message to the server...
	    eval {
		$cli->talk($cmd_res->{SERVICE_COMMAND});
	    };
	    if (my $exc = OpenXPKI::Exception->caught()) {
		print $OUT "Error sending service message.\n";
#		redo INTERACTION;
		exit 1;
	    }
	}

	if (exists $cmd_res->{COMMAND} &&
	    (ref $cmd_res->{COMMAND} eq '') &&
	    (! exists $cmd_res->{PARAMS} || ($cmd_res->{PARAMS} eq 'HASH'))) {

	    ### send command message to the server...
	    ### $cmd->res->{COMMAND}
	    eval {
		$cli->send_command_msg($cmd_res->{COMMAND},
				       $cmd_res->{PARAMS});
	    };
	    if (my $exc = OpenXPKI::Exception->caught()) {
		print $OUT "Error sending service message.\n";
		exit 1;
	    }
	}


	$term->addhistory($line) if ($line ne "");
    }    
    exit 0;

} else {
    die "Non-interactive use currently under construction. Stopped";

    my $res = $cli->process(join(' ', @ARGV));
    my $rc = $res->{ERROR};
    if ($res->{ERROR} == 0) {
	if (scalar @{$res->{MESSAGE}}) {
	    print join("\n", @{$res->{MESSAGE}});
	}
    } else {
	print "ERROR $res->{ERROR}: ". join("\n", @{$res->{MESSAGE}}) . "\n";
    }
    exit $rc;
}

__END__

=head1 NAME

