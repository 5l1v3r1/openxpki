#!/usr/bin/perl
#
# Written by Martin Bartosch for the OpenXPKI project 2006
# Copyright (c) 2006 by The OpenXPKI Project
# $Revision: 80 $
#

our $VERSION = '0.9.$Revision: 244 $';
$VERSION =~ s{ \$ Revision: \s* (\d+) \s* \$ \z }{$1}xms;

use strict;
use warnings;
use English;
use Getopt::Long;
use Pod::Usage;
use File::Spec;

use OpenXPKI::Version;
use OpenXPKI::Server::Init;
use OpenXPKI::Server::Context qw( CTX );

my $prefix          = "@prefix@";
my $exec_prefix     = "@exec_prefix@";
my $sysconfdir      = "@sysconfdir@";

my $configfile      = "$sysconfdir/config.xml";

# read configuration from deployed OpenXPKI instance
sub get_config {
    my $cfgfile = shift;
    return OpenXPKI::Server::Init::init(
	{
	    CONFIG => $cfgfile,
	    TASKS  => [ 
		'xml_config', 
		'i18n', 
		'log', 
		],
	});
}


sub initdb {
    my $args = shift;


    my $type = CTX('xml_config')->get_xpath (
	XPATH    => [ 'common/database/type' ],
	COUNTER  => [ 0 ]);
    
    print STDERR "Database type: $type\n";

    my @databases = qw( log );

    # SQLite needs special treatment: three databases instead of one must
    # be initialized
    if ($type =~ m{ SQLite }xms) {
	push @databases, 'workflow', 'backend';
    }

  DB:
    foreach my $db (@databases) {
	my $params = {};
	if ($db ne 'log') {
	    $params->{PURPOSE} = $db;
	}

	if (defined $params->{PURPOSE}) {
	    print STDERR "Setting up database '$db'\n";
	}
	my $dbi = OpenXPKI::Server::Init::get_dbi($params);

	eval { 
	    $dbi->connect() 
	};
	if ($EVAL_ERROR)
	{
	    print STDERR "ERROR: Could not connect to '$db' database\n";
	    return;
	}
	
	if ($args->{DRYRUN}) {
	    print $dbi->init_schema(MODE => 'DRYRUN') . "\n";
	    last DB;
	} else {
	    if ($args->{FORCE}) {
		print STDERR "ERROR: forced creation not yet implemented.\n";
		return;
	    }

	    eval {
		$dbi->init_schema();
	    };
	    if ($EVAL_ERROR) {
		print STDERR "ERROR: init_schema on '$db' failed (${EVAL_ERROR})\n";
		return;
	    }
	    print STDERR "Database '$db' initialized.\n";
	}
    }
    return 1;
}


sub deploy {
    my $args = shift;

    my $dir         = $args->{DIR};
    my $templatedir = $args->{TEMPLATEDIR};

    if (! defined $dir || ($dir eq '')) {
	print STDERR "No target directory specified.\n";
	return;
    }

    if (! (-d $dir && -r $dir && -x $dir && -w $dir)) {
	print STDERR "Directory $dir does not exist or is not writable.\n";
	return;
    }

    print "Deploying new OpenXPKI instance to target directory $dir.\n";


    return;
}

###########################################################################

my %params;
GetOptions(\%params,
	   qw(
	      help|?
	      man
	      cfg|cfgfile|conf|config=s
              version
	      force
	      dryrun
	      )) or pod2usage(-verbose => 0);

if ($params{version}) {
    my ($vol, $dir, $file) = File::Spec->splitpath($0);
    print "OpenXPKI Core Version: $OpenXPKI::VERSION::VERSION\n";
    print "$file Version: $VERSION\n";
    exit 0;
}

pod2usage(-exitstatus => 0, -verbose => 2) if $params{man};
pod2usage(-verbose => 1) if ($params{help});
if (scalar @ARGV < 1) {
    print STDERR "Usage: openxpkiadm [OPTIONS] COMMAND\n";
    exit 0;
}

if (defined $params{cfg}) {
    $configfile = $params{cfg};
}


###########################################################################

my $cmd = shift;

if ($cmd eq 'initdb') {
    if (! get_config($configfile)) {
	print STDERR "Could not obtain OpenXPKI instance configuration\n";
	exit 1;
    }
    if (! initdb(
	     {
		 DRYRUN => $params{dryrun},
		 FORCE => $params{force},
	     })) {
	print STDERR "Could not initialize database.\n";
	exit 1;
    }
} elsif ($cmd eq 'deploy') {
    my $dir = shift;
    if (! defined $dir) {
	$dir = $sysconfdir;
    }

    if (! deploy(
	{
	    DIR => $dir,
	})) {
	print STDERR "Could not deploy OpenXPKI instance.\n";
	exit 1;
    }
} else {
    print STDERR "Unknown command '$cmd'.\n";
    exit 1;
}

exit 0;

__END__

=head1 NAME

openxpkiadm - tool for management operations of OpenXPKI instances

=head1 USAGE

openxpkiadm [OPTIONS] COMMAND

 Options:
   --help                brief help message
   --man                 full documentation
   --config FILE         use configuration from FILE
   --force               Force operation (may be destructive)
   --dryrun              Don't change anything, just print what would be done

 Commands:
   deploy DIR            Deploy a new OpenXPKI installation to directory DIR
   initdb                Initialize database


=head1 ARGUMENTS

Available commands:

=over 8

=item B<deploy> DIR

Creates a new OpenXPKI server installation including the required 
configuration templates and control scripts to the specified directory.

=item B<initdb>

Initializes the OpenXPKI database schema. Will not destroy existing data
unless called with --force.

=back


=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exits.

=item B<--man>

Prints the manual page and exits.

=item B<--config FILE>

Read configuration file FILE. Uses built-in default if not specified.

=item B<--version>

Print program version and exit.

=item B<--force>

Force execution of command.

WARNING: This may destroy existing data!

=item B<--dryrun>

Prints effects of a command without actually modifying anything.

=back

=head1 DESCRIPTION

B<openxpkiadm> is the administrative frontend for controlling the OpenXPKI
installation.

=over 8

NOTE: This script was customized to the paths specified during 
installation.
You will have to modify this script to reflect any changes to the 
installation directories.

The openxpkiadm script returns a 0 exit value on success, and >0 if  an
error occurs.

=back

